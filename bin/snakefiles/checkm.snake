
#---- CheckM stats -------------------------------------------------------------
CHECKM_ENV = config['checkm_env']

rule checkm_all:
    input:
        expand(bin_dir + "{bin}/checkm/checkm.tsv",
               bin=config['binning_samples'])

rule checkm:
    input:   "{path}/{bin}/maxbin/{bin}.summary"
    output:  qa="{path}/{bin}/checkm/qa.tsv", tree_qa="{path}/{bin}/checkm/tree_qa.tsv"
    params:  in_dir="{path}/{bin}/maxbin", out_dir="{path}/{bin}/checkm"
    log:     "{path}/{bin}/checkm/checkm.log"
    #TODO use already called genes
    shell:   "set +u; {CHECKM_ENV}; set -u \n"
             "checkm tree -x fasta {params.in_dir} {params.out_dir} &> {log} \n"
             "checkm tree_qa -o 2 --tab_table -f {output.tree_qa} {params.out_dir} &> {log} \n"
             "checkm lineage_set {params.out_dir} {params.out_dir}/lineage.ms &> {log} \n"
             "checkm analyze -x fasta {params.out_dir}/lineage.ms {params.in_dir} {params.out_dir} &> {log} \n"
             "checkm qa -o 2 --tab_table -f {output.qa} {params.out_dir}/lineage.ms {params.out_dir} &> {log} \n"
             "source deactivate"

rule parse_checkm:
    input:   qa="{path}/qa.tsv", tree_qa="{path}/tree_qa.tsv"
    output:  "{path}/checkm.tsv"
    run:
        import pandas as pd
        table = pd.read_table(input.qa, dtype="str")
        tree_table = pd.read_table(input.tree_qa, dtype="str", na_filter=False)
        all_table = pd.merge(table, tree_table, on="Bin Id")
        res_table = all_table[["Bin Id", "Taxonomy (contained)", "Taxonomy (sister lineage)", "Genome size (Mbp)", "Completeness", "Contamination"]].copy()
        def extract_taxon(taxonomy):
            return str(taxonomy).split(";")[-1]
        for column in ["Taxonomy (contained)", "Taxonomy (sister lineage)"]:
            res_table[column] = res_table[column].apply(extract_taxon)
        res_table.to_csv(output[0], index=False, sep="\t")
